plugins {
	id 'java'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	mavenCentral()
}

dependencies {
	compileOnly("net.fabricmc:fabric-loader:${project.loader_version}") {
		artifact {
			name = 'fabric-loader'
			type = 'jar'
		}
	}
	compileOnly "org.slf4j:slf4j-api:1.7.25"
	compileOnly "net.java.dev.jna:jna:5.6.0"

	testImplementation("net.fabricmc:fabric-loader:${project.loader_version}") {
		artifact {
			name = 'fabric-loader'
			type = 'jar'
		}
	}
	testImplementation "org.slf4j:slf4j-api:1.7.25"
	testImplementation "org.slf4j:slf4j-simple:1.7.25"
	testImplementation "net.java.dev.jna:jna:5.6.0"
	testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
	useJUnitPlatform()
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}
